# @copyright (c) 2020-2020 RWTH Aachen. All rights reserved.
#
# ddX software
#
# @file CMakeLists.txt
# @version 1.0.0
# @author Aleksandr Mikhalev
# @date 2020-12-17


###############################################################################
#                       THIS IS A TOP-LEVEL CMAKELISTS.txt                    #
#                                                                             #
#        It is intended to find all dependencies (required or optional)       #
#                    and set up corresponding variables                       #
###############################################################################


###############################################################################
##                            PRELIMINARIES                                  ##
###############################################################################

# Need to identify lowest possible CMake version
cmake_minimum_required(VERSION 3.2.3)

# Restrict building in top-level directory
if("${CMAKE_SOURCE_DIR}" STREQUAL "${CMAKE_BINARY_DIR}")
    message(FATAL_ERROR "In-source builds are not allowed.\nPlease create a "
        "build directory first and execute cmake configuration from this "
        "directory. Example: mkdir build && cd build && cmake ..")
endif()

# Read version
file(STRINGS "VERSION.txt" VERSION LIMIT_COUNT 1)

# Notify user about project name and version
message(STATUS "Configuring ddX ${VERSION}")

# Create project and check Fortran compiler
project(ddX VERSION ${VERSION} LANGUAGES Fortran CXX)

# Extend path for additional cmake modules
list(APPEND CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake")

###############################################################################
##                REQUIREMENTS: OpenMP, BLAS AND LAPACK                      ##
###############################################################################

find_package(BLAS REQUIRED)
find_package(LAPACK REQUIRED)

###############################################################################
##                           DEFINE OPTIONS                                  ##
###############################################################################

# Options for build type for a convenient ccmake use
set_property(CACHE CMAKE_BUILD_TYPE PROPERTY STRINGS
    None Debug Release RelWithDebInfo MinSizeRel)

# Build tests
option(TESTS "Generate testing binaries" ON)

# Build documentation
option(DOCS "Build documentation" ON)

# Check if a code coverage report is needed
option(COVERAGE "Generate code coverage report" OFF)

# Whether or not to use OpenMP
option(OPENMP "Enable OpenMP" ON)

###############################################################################
##                               CHECK OPTIONS                               ##
###############################################################################

# Check if need to build documentation
if(DOCS)
    find_package(Doxygen)
    if(NOT DOXYGEN_FOUND)
        set(DOCS OFF)
    endif()
endif()

# Update compiler flags in case of code coverage
if(COVERAGE)
    # Tell user what we are doing here
    message(STATUS "Code coverage report was requested, so option TESTS is ON."
        " The report itself can be generated by \"make coverage\" command.")
    # Enable tests even if they were manually disabled
    set(TESTS ON)
    # Use CodeCoverage.cmake from cmake_modules
    include(CodeCoverage)
    # Append coverage flags
    append_coverage_compiler_flags()
    # Set excluded coverage paths
    set(COVERAGE_EXCLUDES "tests/*" "src/llgnew.f" "src/bessel.f90")
    # Setup a target for an overall coverage
    setup_target_for_coverage_lcov(NAME coverage
        EXECUTABLE ctest
        LCOV_ARGS --no-external)
endif()

# Check if testing is required
if(TESTS)
    include_directories(${CMAKE_BINARY_DIR}/src)
    enable_testing()
    if(APPLE)
        if(DEFINED ADD_RPATH)
            message(STATUS "RPATH of each test will be extended by "
                "${ADD_RPATH}")
        else()
            message(STATUS "`make test` command might not work properly on "
                "Apple system due to System Integrity Protection (SIP). A "
                "walkaround is based on providing paths with dynamic libraries"
                " through -DADD_RPATH additional argument to the cmake "
                "configuration command.")
        endif()
    endif()
endif()

# Check OpenMP option
if(OPENMP)
    #message(WARNING "OpenMP is currently disabled, setting OPENMP to OFF")
    #set(OPENMP OFF)
    find_package(OpenMP REQUIRED)
    add_definitions(-DOPENMP)
endif()

###############################################################################
##          AUXILIARY FUNCTION TO COPE WITH UNIQUE LOGICAL NAMES             ##
###############################################################################

# Add single test executable, presented by its single source, along with a list
# of combinations of parameters (stored as strings). One test executable = one
# source file. Number of tests = number of combinations. These tests are then
# available for ctest.
function(ddx_add_test src)
    # Parse path to get name of executable and its corresponding unique logical
    # name, based on its path and name
    get_filename_component(exec ${src} NAME_WE)
    file(RELATIVE_PATH path ${CMAKE_SOURCE_DIR} ${CMAKE_CURRENT_SOURCE_DIR})
    string(REPLACE "/" "_" suffix ${path})
    set(logical ${suffix}_${exec})
    # Register executable with its unique logical name
    add_executable(${logical} ${src})
    # Set name of executable file
    set_target_properties(${logical} PROPERTIES OUTPUT_NAME ${exec})
    # Add dependency to the global coverage target
    if(COVERAGE)
        add_dependencies(coverage ${logical})
    endif()
    # Try to set environment for a test on Apple MacOS
    if(APPLE AND DEFINED ADD_RPATH)
        set_target_properties(${logical} PROPERTIES BUILD_RPATH "${ADD_RPATH}")
    endif()
    # Link to ddx
    target_link_libraries(${logical} ddx)
    if(COVERAGE)
        # Add coverage for all tests together (they have ${logical}_${i} names)
        setup_target_for_coverage_lcov(NAME coverage_${logical}
            EXECUTABLE ctest -R ${logical}
            DEPENDENCIES ${logical}
            LCOV_ARGS --no-external)
    endif()
    # If no arguments were provided, add test without parameters
    if(${ARGC} EQUAL 1)
        add_test(NAME ${logical} COMMAND ${exec})
        message(STATUS "Adding 1 test: ${path}/${exec}")
    # If only one test with arguments must be added
    elseif(${ARGC} EQUAL 2)
        # Convert arguments from string to list
        string(REPLACE " " ";" param ${ARGV1})
        add_test(NAME ${logical} COMMAND ${exec} ${param})
        message(STATUS "Adding 1 test: ${path}/${exec}")
    # Add multiple tests and add indexing for better readability of output
    else()
        math(EXPR ntests ${ARGC}-1)
        message(STATUS "Adding ${ntests} tests: ${path}/${exec}")
        # For every string of parameters, register corresponding test
        foreach(i RANGE 1 ${ntests})
            # Convert arguments from string to list
            string(REPLACE " " ";" param ${ARGV${i}})
            add_test(NAME ${logical}_${i} COMMAND ${exec} ${param})
        endforeach()
    endif()
endfunction(ddx_add_test)

###############################################################################
##                 BUILD LIBRARY, EXAMPLES, TESTS AND DOCS                   ##
###############################################################################

add_subdirectory("src")

# Build examples
if(EXAMPLES)
    add_subdirectory("examples")
endif()

# Build tests
if(TESTS)
    add_subdirectory("tests")
endif()

# Build documentation
if(DOCS)
    add_subdirectory("docs")
endif()


